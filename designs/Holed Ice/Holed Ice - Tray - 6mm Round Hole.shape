// ShapeScript document

/// Straw Ice - Tray â€“ 6mm

detail 100
rotate 0 0 -0.5  /// CAD mode

define STRAWDIAMETER      6     /// mm
define ICEBLOCKDIAMETER   (STRAWDIAMETER * 4)  /// mm
define ICEBLOCKHEIGHT     (ICEBLOCKDIAMETER / 1.5)  /// mm
define TRAYWALLTHICKNESS  2     /// mm
define TRAYBLOCKMOLDGAP   (TRAYWALLTHICKNESS * 2)  /// mm
define TRAYBLOCKCOUNTX    4  /// count
define TRAYBLOCKCOUNTY    6  /// count
define TRAYWIDTH          (((ICEBLOCKDIAMETER + TRAYWALLTHICKNESS * 2 + TRAYBLOCKMOLDGAP) * TRAYBLOCKCOUNTX) + TRAYBLOCKMOLDGAP)  /// mm
define TRAYLENGTH         (((ICEBLOCKDIAMETER + TRAYWALLTHICKNESS * 2 + TRAYBLOCKMOLDGAP) * TRAYBLOCKCOUNTY) + TRAYBLOCKMOLDGAP)  /// mm
define TRAYCORNERRADIUS   (ICEBLOCKDIAMETER + TRAYWALLTHICKNESS * 2 + TRAYBLOCKMOLDGAP * 2)  /// mm

define ICEBLOCK {
    cylinder {
        size ICEBLOCKDIAMETER  ICEBLOCKHEIGHT
    }
}

define TOP {
    difference {
        union {
            cube {
                size (TRAYWIDTH - TRAYCORNERRADIUS)  (TRAYWALLTHICKNESS * 2)  (TRAYLENGTH)
            }
            cube {
                size (TRAYWIDTH)  (TRAYWALLTHICKNESS * 2)  (TRAYLENGTH - TRAYCORNERRADIUS)
            }
            cylinder {
                size TRAYCORNERRADIUS  (TRAYWALLTHICKNESS * 2)
                position ((TRAYWIDTH - TRAYCORNERRADIUS) / 2)  0  ((TRAYLENGTH - TRAYCORNERRADIUS) / 2)
            }
            cylinder {
                size TRAYCORNERRADIUS  (TRAYWALLTHICKNESS * 2)
                position -((TRAYWIDTH - TRAYCORNERRADIUS) / 2)  0  ((TRAYLENGTH - TRAYCORNERRADIUS) / 2)
            }
            cylinder {
                size TRAYCORNERRADIUS  (TRAYWALLTHICKNESS * 2)
                position ((TRAYWIDTH - TRAYCORNERRADIUS) / 2)  0  -((TRAYLENGTH - TRAYCORNERRADIUS) / 2)
            }
            cylinder {
                size TRAYCORNERRADIUS  (TRAYWALLTHICKNESS * 2)
                position -((TRAYWIDTH - TRAYCORNERRADIUS) / 2)  0  -((TRAYLENGTH - TRAYCORNERRADIUS) / 2)
            }
        }

        union {
            position 0  TRAYWALLTHICKNESS / 2  0

            cube {
                size (TRAYWIDTH - TRAYCORNERRADIUS)  (TRAYWALLTHICKNESS)  (TRAYLENGTH - TRAYWALLTHICKNESS * 2)
            }
            cube {
                size (TRAYWIDTH - TRAYWALLTHICKNESS * 2)  (TRAYWALLTHICKNESS)  (TRAYLENGTH - TRAYCORNERRADIUS)
            }
            cylinder {
                size (TRAYCORNERRADIUS - TRAYWALLTHICKNESS * 2)  (TRAYWALLTHICKNESS)
                position ((TRAYWIDTH - TRAYCORNERRADIUS) / 2)  0  ((TRAYLENGTH - TRAYCORNERRADIUS) / 2)
            }
            cylinder {
                size (TRAYCORNERRADIUS - TRAYWALLTHICKNESS * 2)  (TRAYWALLTHICKNESS)
                position -((TRAYWIDTH - TRAYCORNERRADIUS) / 2)  0  ((TRAYLENGTH - TRAYCORNERRADIUS) / 2)
            }
            cylinder {
                size (TRAYCORNERRADIUS - TRAYWALLTHICKNESS * 2)  (TRAYWALLTHICKNESS)
                position ((TRAYWIDTH - TRAYCORNERRADIUS) / 2)  0  -((TRAYLENGTH - TRAYCORNERRADIUS) / 2)
            }
            cylinder {
                size (TRAYCORNERRADIUS - TRAYWALLTHICKNESS * 2)  (TRAYWALLTHICKNESS)
                position -((TRAYWIDTH - TRAYCORNERRADIUS) / 2)  0  -((TRAYLENGTH - TRAYCORNERRADIUS) / 2)
            }
        }
    }
}

define TRAY {
    difference {
        /// Base
        union {
            // Mold bodies
            for i in 0 to (TRAYBLOCKCOUNTX - 1) {
                for j in 0 to (TRAYBLOCKCOUNTY - 1) {
                    cylinder {
                        size (ICEBLOCKDIAMETER + TRAYWALLTHICKNESS * 2)  (ICEBLOCKHEIGHT + TRAYWALLTHICKNESS)
                        position ((ICEBLOCKDIAMETER + TRAYWALLTHICKNESS * 2 + TRAYBLOCKMOLDGAP) * i)  -(TRAYWALLTHICKNESS / 2)  ((ICEBLOCKDIAMETER + TRAYWALLTHICKNESS * 2 + TRAYBLOCKMOLDGAP) * j)
                    }
                }
            }

            /// Top of the mold
            TOP {
                position ((TRAYWIDTH / 2) - (ICEBLOCKDIAMETER / 2 + TRAYWALLTHICKNESS + TRAYBLOCKMOLDGAP))  (ICEBLOCKHEIGHT / 2)  ((TRAYLENGTH / 2) - (ICEBLOCKDIAMETER / 2 + TRAYWALLTHICKNESS + TRAYBLOCKMOLDGAP))
            }
        }

        /// Mold cut-outs
        union {
            for i in 0 to (TRAYBLOCKCOUNTX - 1) {
                for j in 0 to (TRAYBLOCKCOUNTY - 1) {
                    ICEBLOCK {
                        position ((ICEBLOCKDIAMETER + TRAYWALLTHICKNESS*2 + TRAYBLOCKMOLDGAP) * i)  0  ((ICEBLOCKDIAMETER + TRAYWALLTHICKNESS*2 + TRAYBLOCKMOLDGAP) * j)
                    }
                }
            }
        }
    }
}

TRAY {
    opacity 0.75
}
