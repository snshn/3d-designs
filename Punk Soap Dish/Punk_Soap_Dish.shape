// ShapeScript document

// Name: Punk Soap Dish
// Author: Sunshine
// Designed on: 05/26/2021

detail 4

define x 4
define y 6
define pyramidHeight 8
define pyramidWidth 19
define pyramidConeRadius pyramidWidth * sqrt(2)
define platformHeight 4

define soapDishPyramid {
    cone {
        detail 4
        orientation 0 0.25 0
        size pyramidConeRadius  pyramidHeight
    }
}

define soapDish {
    // Platform
    cube {
        // position 66.5-19  -platformHeight / 2  28.5
        position pyramidWidth + pyramidWidth/2  -platformHeight / 2  pyramidWidth*2 + pyramidWidth/2
        size pyramidWidth*x  platformHeight  pyramidWidth*y
    }

    // Pyramids
    for i in 0 to x-1 {
        for j in 0 to y-1 {
            soapDishPyramid {
                position i * pyramidWidth  pyramidHeight / 2  j * pyramidWidth
            }
        }
    }
}

soapDish {
    orientation 0.5 1 0.5
}
